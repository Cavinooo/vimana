load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_go//go:def.bzl", "go_library", "go_test")

go_library(
    name = "controller",
    srcs = [
        "component_controller.go",
        "domain_controller.go",
        "server_controller.go",
        "vimana_controller.go",
    ],
    importpath = "vimana.host/operator/internal/controller",
    visibility = ["//operator/cmd:__pkg__"],
    deps = [
        "//operator/api/v1alpha1",
        "@io_k8s_apimachinery//pkg/api/errors",
        "@io_k8s_apimachinery//pkg/api/meta",
        "@io_k8s_apimachinery//pkg/apis/meta/v1:meta",
        "@io_k8s_apimachinery//pkg/runtime",
        "@io_k8s_apimachinery//pkg/types",
        "@io_k8s_sigs_controller_runtime//:controller-runtime",
        "@io_k8s_sigs_controller_runtime//pkg/client",
        "@io_k8s_sigs_controller_runtime//pkg/handler",
        "@io_k8s_sigs_controller_runtime//pkg/log",
        "@io_k8s_sigs_controller_runtime//pkg/reconcile",
        "@io_k8s_sigs_gateway_api//apis/v1:apis",
        "@io_k8s_utils//ptr",
    ],
)

go_test(
    name = "controller_test",
    srcs = [
        "component_controller_test.go",
        "domain_controller_test.go",
        "server_controller_test.go",
        "suite_test.go",
        "vimana_controller_test.go",
    ],
    # Disable ANSI-escaped output in the test log.
    args = ["-ginkgo.no-color"],
    data = [
        # These dependencies would normally be downloaded by `setup-envtest`:
        # https://pkg.go.dev/sigs.k8s.io/controller-runtime/tools/setup-envtest
        ":etcd-asset",
        ":kube-apiserver-asset",
        "//operator/config/crd/bases",
        # Need to manually install the Gateway API CRDs for the operators to function.
        ":gateway-api",
    ],
    embed = [":controller"],
    deps = [
        "//operator/api/v1alpha1",
        "@com_github_onsi_ginkgo_v2//:ginkgo",
        "@com_github_onsi_gomega//:gomega",
        "@io_k8s_apimachinery//pkg/api/errors",
        "@io_k8s_apimachinery//pkg/apis/meta/v1:meta",
        "@io_k8s_apimachinery//pkg/types",
        "@io_k8s_client_go//kubernetes/scheme",
        "@io_k8s_client_go//rest",
        "@io_k8s_sigs_controller_runtime//pkg/client",
        "@io_k8s_sigs_controller_runtime//pkg/envtest",
        "@io_k8s_sigs_controller_runtime//pkg/log",
        "@io_k8s_sigs_controller_runtime//pkg/log/zap",
        "@io_k8s_sigs_controller_runtime//pkg/reconcile",
        "@io_k8s_sigs_gateway_api//apis/v1:apis",
    ],
)

# Bazel adds the `.exe` suffix to all executable files (even on Linux),
# but the controller runtime's `envtest` expects to find binaries without the suffix.
# Use `copy_file` to rename them.

copy_file(
    name = "etcd-asset",
    src = "@rules_k8s//:etcd",
    # The test harness expects to find the `etcd` binary at this path.
    out = "assets/etcd",
    allow_symlink = True,
    is_executable = True,
)

copy_file(
    name = "kube-apiserver-asset",
    src = "@rules_k8s//:kube-apiserver",
    # The test harness expects to find the `kube-apiserver` binary at this path.
    out = "assets/kube-apiserver",
    allow_symlink = True,
    is_executable = True,
)

copy_file(
    name = "gateway-api",
    src = "@rules_k8s//:gateway-api",
    # The test harness expects to find the Gateway API's CRD installation file
    # under the parent `apis/`.
    out = "apis/gateway.yaml",
    allow_symlink = True,
)
