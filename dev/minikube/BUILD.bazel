load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")

# Name of some registry we can push / pull images to / from,
# from the execution platform's perspective.
local_registry = "localhost:5000"

# Name of the same registry as `local_registry`,
# as it would appear from inside the cluster.
cluster_registry = "host.minikube.internal:5000"

kicbase_image_name = "kicbase-vimanad"

kicbase_tag = "latest"

kicbase_local_repo = local_registry + "/" + kicbase_image_name + ":" + kicbase_tag

# Start a Vimana-enabled minikube cluster locally
# with the latest builds of the runtime and operator from source.
# If Minikube is already running, shut it down first.
sh_binary(
    name = "restart",
    srcs = ["restart.sh"],
    args = [
        "$(location @rules_k8s//:kubectl)",
        "$(location @rules_k8s//:helm)",
        "$(location @rules_k8s//:minikube)",
        "$(location @rules_k8s//:minikube-bin)",
        "$(location :kicbase-image-push-local)",
        "$(location //operator:image-push-local)",
        kicbase_local_repo,
        cluster_registry,
        "$(location //operator:deploy)",
    ],
    data = [
        ":kicbase-image-push-local",
        "//operator:deploy",
        "//operator:image-push-local",
        "@rules_k8s//:helm",
        "@rules_k8s//:kubectl",
        "@rules_k8s//:minikube",
        "@rules_k8s//:minikube-bin",
    ],
    # Customize kubeconfig output path by setting `KUBECONFIG`.
    # The default is `${HOME}/.kube/config`.
    env_inherit = [
        "KUBECONFIG",
        "HOME",
    ],
    deps = ["//dev:bash-util"],
)

# If there's a running minikube cluster started by `:restart`,
# Deploy fresh builds of both the container runtime and API operator in it
# without a full reset.
# This can significantly speed up iteration,
# but be mindful of the safety disclaimer at the top of this script!
sh_binary(
    name = "hotswap",
    srcs = ["hotswap.sh"],
    args = [
        "$(location @rules_k8s//:minikube)",
        "$(location @rules_k8s//:minikube-bin)",
        "$(location @rules_k8s//:kubectl)",
        "$(location //cluster/node:vimanad-x86_64-linux)",
        "$(location //operator:image-push-local)",
        "$(location //operator:deploy)",
    ],
    data = [
        "//cluster/node:vimanad-x86_64-linux",
        "//dev:bash-util",
        "//operator:deploy",
        "//operator:image-push-local",
        "@rules_k8s//:kubectl",
        "@rules_k8s//:minikube",
        "@rules_k8s//:minikube-bin",
    ],
)

# Kicbase (Kubernetes In (a) Container? base) is like the node image for Minikube.
oci_image(
    name = "kicbase-image",
    base = "@kicbase",
    # Layer `vimanad` on top of it to enable Vimana on Minikube.
    tars = [":kicbase-vimanad-tar"],
)

oci_push(
    name = "kicbase-image-push-local",
    image = ":kicbase-image",
    remote_tags = [kicbase_tag],
    repository = local_registry + "/" + kicbase_image_name,
)

oci_load(
    name = "kicbase-image-load",
    image = ":kicbase-image",
    repo_tags = [kicbase_image_name + ":" + kicbase_tag],
)

pkg_tar(
    name = "kicbase-vimanad-tar",
    srcs = [
        # Vimanad SystemD unit file.
        ":vimanad.kicbase.service",
        # Vimanad binary built for the container.
        "//cluster/node:vimanad-x86_64-linux",
        # We need a more modern version of `crictl` than the one that ships with Kicbase by default.
        "@rules_k8s//:crictl-x86_64-linux",
        # Standard `host-local` IPAM CNI plugin.
        "@rules_k8s//:host-local-x86_64-linux",
    ],
    remap_paths = {
        "/vimanad.kicbase.service": "/etc/systemd/system/vimanad.service",
        "/vimanad-x86_64-linux": "/usr/bin/vimanad",
        # Override these things that should already be on the image,
        # because we want specific versions.
        # Bazel downloader adds the `.exe` suffix no matter what the platform is.
        "/crictl-x86_64-linux.exe": "/usr/bin/crictl",
        "/host-local-x86_64-linux.exe": "/opt/cni/bin/host-local",
    },
)
