load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_shell//shell:sh_binary.bzl", "sh_binary")

# Name of some registry we can push / pull images to / from.
local_registry = "localhost:5000"

# Name of the same registry as `local_registry`,
# as it would appear from inside the cluster.
cluster_registry = "host.minikube.internal:5000"

kicbase_image_name = "kicbase-workd"

kicbase_tag = "latest"

kicbase_local_repo = local_registry + "/" + kicbase_image_name + ":" + kicbase_tag

# Start a Vimana-enabled minikube cluster locally
# with the latest build of `workd` from source.
# If Minikube is already running, shut it down first.
sh_binary(
    name = "restart",
    srcs = ["restart.sh"],
    args = [
        "$(location @rules_k8s//:kubectl)",
        "$(location @rules_k8s//:istioctl)",
        "$(location @rules_k8s//:minikube)",
        "$(location @rules_k8s//:minikube-bin)",
        "$(location :kicbase-image-push-local)",
        kicbase_local_repo,
        cluster_registry,
    ],
    data = [
        ":kicbase-image-push-local",
        "@rules_k8s//:istioctl",
        "@rules_k8s//:kubectl",
        "@rules_k8s//:minikube",
        "@rules_k8s//:minikube-bin",
    ],
    deps = ["//dev:bash-util"],
)

# If there's a running minikube cluster started by `:restart`,
# this action uploads a fresh build of `workd` to the running kicbase container
# then restarts the daemon.
sh_binary(
    name = "hotswap",
    srcs = ["hotswap.sh"],
    args = [
        "$(location @rules_k8s//:minikube)",
        "$(location @rules_k8s//:minikube-bin)",
        "$(location @rules_k8s//:kubectl)",
        "$(location //cluster/node:workd-x86_64-linux)",
    ],
    data = [
        "//cluster/node:workd-x86_64-linux",
        "//dev:bash-util",
        "@rules_k8s//:kubectl",
        "@rules_k8s//:minikube",
        "@rules_k8s//:minikube-bin",
    ],
)

# Kicbase (Kubernetes In (a) Container base?) is like the node image for Minikube.
oci_image(
    name = "kicbase-image",
    base = "@kicbase",
    # Layer `workd` on top of it to enable Vimana on Minikube.
    tars = [":kicbase-workd-tar"],
)

oci_push(
    name = "kicbase-image-push-local",
    image = ":kicbase-image",
    remote_tags = [kicbase_tag],
    repository = local_registry + "/" + kicbase_image_name,
)

oci_load(
    name = "kicbase-image-load",
    image = ":kicbase-image",
    repo_tags = [kicbase_image_name + ":" + kicbase_tag],
)

pkg_tar(
    name = "kicbase-workd-tar",
    srcs = [
        # Workd SystemD unit file.
        ":workd.kicbase.service",
        # Workd binary built for the container.
        "//cluster/node:workd-x86_64-linux",
        # We need a more modern version of `crictl` than the one that ships with Kicbase by default.
        "@rules_k8s//:crictl-x86_64-linux",
        # Standard `host-local` IPAM CNI plugin.
        "@rules_k8s//:host-local-x86_64-linux",
    ],
    remap_paths = {
        "/workd.kicbase.service": "/etc/systemd/system/workd.service",
        "/workd-x86_64-linux": "/usr/bin/workd",
        # Override these things that should already be on the image,
        # because we want specific versions.
        # Bazel downloader adds the `.exe` suffix no matter what the platform is.
        "/crictl-x86_64-linux.exe": "/usr/bin/crictl",
        "/host-local-x86_64-linux.exe": "/opt/cni/bin/host-local",
    },
)
